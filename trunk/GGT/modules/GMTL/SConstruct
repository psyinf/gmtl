#!python

EnsureSConsVersion(0,96)
SConsignFile()

import os, string, sys
import re
import distutils.sysconfig
import distutils.util
import SCons
import SCons.Util

pj = os.path.join

# Bring in the AutoDist build helper
sys.path.append('tools/build')
from AutoDist import *

# True and False were not defined prior to Python 2.2.1.
if sys.version[:3] == '2.2' and sys.version[3] != '.':
   False = 0
   True  = 1

enable_python      = False
boost_version      = '1.31'
have_cppunit       = False
compiler_major_ver = 0

try: has_help_flag = SCons.Script.Main.options.help_msg
except AttributeError: has_help_flag = SCons.Script.options.help_msg

#------------------------------------------------------------------------------
# Define some generally useful functions
#------------------------------------------------------------------------------
def GetGMTLVersion():
   "Gets the GMTL version from the gmtl/Version.h header"

   contents = open('gmtl/Version.h', 'r').read()
   major = re.compile('.*(#define *GMTL_VERSION_MAJOR *(\d+)).*', re.DOTALL).sub(r'\2', contents)
   minor = re.compile('.*(#define *GMTL_VERSION_MINOR *(\d+)).*', re.DOTALL).sub(r'\2', contents)
   patch = re.compile('.*(#define *GMTL_VERSION_PATCH *(\d+)).*', re.DOTALL).sub(r'\2', contents)
   return (int(major), int(minor), int(patch))

def GetPlatform():
   "Determines what platform this build is being run on."
   if string.find(sys.platform, 'irix') != -1:
      return 'irix'
   elif string.find(sys.platform, 'linux') != -1:
      return 'linux'
   elif string.find(sys.platform, 'freebsd') != -1:
      return 'freebsd'
   elif string.find(sys.platform, 'darwin') != -1:
      return 'darwin'
   elif string.find(sys.platform, 'cygwin') != -1:
      return 'cygwin'
   elif string.find(os.name, 'win32') != -1:
      return 'win32'
   else:
      return sys.platform
Export('GetPlatform')

def CreateConfig(target, source, env):
   "Creates the xxx-config file users use to compile against this library"

   targets = map(lambda x: str(x), target)
   sources = map(lambda x: str(x), source)

   submap = env['submap']

   # Build each target from its source
   for i in range(len(targets)):
      print "Generating config file " + targets[i]
      contents = open(sources[i], 'r').read()

      # Go through the substitution dictionary and modify the contents read in
      # from the source file
      for key, value in submap.items():
         contents = re.sub(re.escape(key), re.escape(value), contents)

      # Write out the target file with the new contents
      open(targets[0], 'w').write(contents)
      os.chmod(targets[0], 0755)
   return 0

def BuildLinuxEnvironment():
   "Builds a base environment for other modules to build on set up for linux"
   global optimize, profile, builders

   CXX = WhereIs('g++3') or 'g++'
   LINK = CXX
   CXXFLAGS = ['-ftemplate-depth-256', '-Wall', '-pipe']
   LINKFLAGS = []

   # Enable profiling?
   if profile != 'no':
      CXXFLAGS.extend(['-pg'])
      LINKFLAGS.extend(['-pg'])

   # Debug or optimize build?
   if optimize != 'no':
      CXXFLAGS.extend(['-DNDEBUG', '-g', '-O3'])
   else:
      CXXFLAGS.extend(['-D_DEBUG', '-g'])

   return Environment(
      ENV         = os.environ,

      CXX         = CXX,
      CXXFLAGS    = CXXFLAGS,
      LINK        = LINK,
      LINKFLAGS   = LINKFLAGS,
      CPPPATH     = [],
      LIBPATH     = [],
      LIBS        = [],
   )

def BuildCygwinEnvironment():
   "Builds a base environment for other modules to build on set up for Cygwin"
   global optimize, profile, builders

   CXX = 'g++'
   LINK = CXX
   CXXFLAGS = ['-ftemplate-depth-256', '-Wall', '-pipe']
   LINKFLAGS = []

   # Enable profiling?
   if profile != 'no':
      CXXFLAGS.extend(['-pg'])
      LINKFLAGS.extend(['-pg'])

   # Debug or optimize build?
   if optimize != 'no':
      CXXFLAGS.extend(['-DNDEBUG', '-g', '-O3'])
   else:
      CXXFLAGS.extend(['-D_DEBUG', '-g'])

   env = Environment(ENV=os.environ, CXX=CXX, LINK=LINK)
   env.Append(CXXFLAGS=CXXFLAGS, LINKFLAGS=LINKFLAGS)
   return env

def BuildDarwinEnvironment():
   "Builds a base environment for other modules to build on set up for Darwin."
   global optimize, profile, builders, compiler_major_ver

   exp = re.compile('^(.*)\/Python\.framework.*$')
   m = exp.search(distutils.sysconfig.get_config_var('prefix'))
   framework_opt = '-F' + m.group(1)

   CXX = WhereIs('g++')

   ver_re = re.compile(r'gcc version ((\d+)\.(\d+)\.(\d+))')
   (gv_stdout, gv_stdin, gv_stderr) = os.popen3(CXX + ' -v')
   ver_str = gv_stderr.read()

   match_obj = ver_re.search(ver_str)

   LINK = CXX
   CXXFLAGS = ['-ftemplate-depth-256', '-DBOOST_PYTHON_DYNAMIC_LIB',
               '-Wno-long-double', '-no-cpp-precomp', '-Wall', framework_opt,
               '-pipe']

   compiler_major_ver = int(match_obj.group(2))

   # GCC 4.0 in Mac OS X 10.4 and newer does not have -fcoalesce-templates.
   if compiler_major_ver < 4:
      CXXFLAGS.append('-fcoalesce-templates')

   SHLIBSUFFIX = distutils.sysconfig.get_config_var('SO')
   SHLINKFLAGS = ['-bundle', framework_opt, '-framework', 'Python']
   LINKFLAGS = []

   # Enable profiling?
   if profile != 'no':
      CXXFLAGS.extend(['-pg'])
      LINKFLAGS.extend(['-pg'])

   # Debug or optimize build?
   if optimize != 'no':
      CXXFLAGS.extend(['-DNDEBUG', '-O3'])
   else:
      CXXFLAGS.extend(['-D_DEBUG', '-g'])

   return Environment(
      ENV         = os.environ,

      CXX         = CXX,
      CXXFLAGS    = CXXFLAGS,
      LINK        = LINK,
      LINKFLAGS   = LINKFLAGS,
      CPPPATH     = [],
      LIBPATH     = [],
      LIBS        = [],
      SHLINKFLAGS = SHLINKFLAGS,
      SHLIBSUFFIX = SHLIBSUFFIX,
   )

def BuildIRIXEnvironment():
   "Builds a base environment for other modules to build on set up for IRIX"
   global optimize, profile, builders

   CXX = 'CC'
   LINK = 'CC'
   CXXFLAGS = ['-n32', '-mips3', '-LANG:std', '-w2']
   LINKFLAGS = CXXFLAGS

   # Enable profiling?
   if profile != 'no':
      CXXFLAGS.extend([])
      LINKFLAGS.extend([])

   # Debug or optimize build?
   if optimize != 'no':
      CXXFLAGS.extend(['-DNDEBUG', '-O2'])
   else:
      CXXFLAGS.extend(['-D_DEBUG', '-g', '-gslim'])

   return Environment(
      ENV         = os.environ,
      CXX         = CXX,
      CXXFLAGS    = CXXFLAGS,
      LINK        = LINK,
      LINKFLAGS   = LINKFLAGS,
      CPPPATH     = [],
      LIBPATH     = [],
      LIBS        = [],
   )

def BuildWin32Environment():
   global optimize, compiler_major_ver

   env = Environment(ENV = os.environ)

   ver_re = re.compile(r'Compiler Version ((\d+)\.(\d+)\.(\d+))')

   # CL.EXE does not actually have a /v option, but using /? would use the
   # build to hang forever waiting on user input to page through the output.
   # XXX: How can env['CXX'] be used in the call to os.popen3()?
   (cv_stdout, cv_stdin, cv_stderr) = os.popen3('cl /v')
   ver_str = cv_stderr.read()

   match_obj = ver_re.search(ver_str)
   if match_obj is None:
      compiler_major_ver = 1300
   else:
      compiler_major_ver = int(match_obj.group(2))

   # We need exception handling support turned on for Boost.Python.
   env.Append(LINKFLAGS = ['/subsystem:console', '/incremental:no'])
   env.Append(CXXFLAGS = ['/Zm800', '/EHsc', '/GR', '/Zc:wchar_t,forScope',
                          '/DBOOST_PYTHON_DYNAMIC_LIB'])

   if compiler_major_ver < 14:
      env.Append(CXXFLAGS = '/Op')

   if optimize != 'no':
      if compiler_major_ver < 14:
         env.Append(CXXFLAGS = '/Ogity')
      else:
         env.Append(CXXFLAGS = '/Oity')

      env.Append(CXXFLAGS = '/O2 /Gs /Ob2 /MD /D_OPT /DNDEBUG'.split())
      env.Append(LINKFLAGS = ['/RELEASE'])
   else:   
      env.Append(CXXFLAGS = ['/Z7', '/Od', '/Ob0', '/MDd', '/D_DEBUG'])
      env.Append(LINKFLAGS = ['/DEBUG'])

   return env

def BuildPlatformEnv():
   env = None

   # Create and export the base environment
   if GetPlatform() == 'irix':
      env = BuildIRIXEnvironment()
   elif GetPlatform() == 'linux' or GetPlatform()[:7] == 'freebsd':
      env = BuildLinuxEnvironment()
   elif GetPlatform() == 'darwin':
      env = BuildDarwinEnvironment()
   elif GetPlatform() == 'win32':
      env = BuildWin32Environment()
   elif GetPlatform() == 'cygwin':
      env = BuildCygwinEnvironment()
   else:
      print 'Unsupported build environment: ' + GetPlatform(), "Trying default"
      env = Environment()

   return env
      
      
# ########################################
# Options
# ########################################
def ValidateBoostVersion(key, value, environ):
   global boost_version

   if "BoostVersion" == key:
      exp = re.compile('^(\d+\.\d+(\.\d+)?)\D*$')
      match = exp.search(value)
      boost_version = match.group(1)
      print "Using Boost version", boost_version
   else:
      assert False, "Invalid Boost key"

def ValidateBoostOption(key, value, environ):
   "Validate the boost option settings"
   global enable_python, optimize, compiler_major_ver
   req_boost_version = 103100
   sys.stdout.write("checking for %s [%s]...\n" % (key, value))

   if "BoostPythonDir" == key:
      boost_inc_dir = pj(value, 'include')

      # Get the boost version.
      boost_ver_filename = pj(boost_inc_dir, 'boost', 'version.hpp')
      if not os.path.isfile(boost_ver_filename):
         sys.stdout.write("%s not found.\n" % boost_ver_filename)

         # Try the versioned Boost include path.
         version_dir = 'boost-' + re.sub(r'\.', '_', boost_version)
         boost_inc_dir = pj(value, 'include', version_dir)
         boost_ver_filename = pj(boost_inc_dir, 'boost', 'version.hpp')

         sys.stdout.write("Trying %s\n" % boost_ver_filename)

         if not os.path.isfile(boost_ver_filename):
            sys.stdout.write("%s not found.\n" % boost_ver_filename)
            enable_python = False
            return False

      ver_file = file(boost_ver_filename)

      # Matches 103000
      ver_num = int(re.search("define\s+?BOOST_VERSION\s+?(\d*)",
                              ver_file.read()).group(1))
      sys.stdout.write("found version: %s\n" % ver_num)

      if ver_num < req_boost_version:
         print "   Boost version too old!  Required version: %s" % req_boost_version
         Exit()
         return False

      # Check on the libraries that I need to use
      if enable_python:
         platform = GetPlatform()

         if platform == 'win32':
            if compiler_major_ver < 14:
               tool = '-vc71'
            else:
               tool = '-vc80'
         elif platform == 'irix':
            tool = '-mp'
         elif platform == 'darwin':
            tool = ''
         else:
            tool = '-gcc'

         if platform == 'darwin':
            threading = ''
         else:
            threading = '-mt'

         if optimize == 'no':
            if platform == 'win32':
               dbg = '-gd'
            else:
               dbg = '-d'
         else:
            dbg = ''

         if platform == 'win32':
            shlib_prefix = ''
            shlib_ext = 'dll'
         elif platform == 'darwin':
            shlib_prefix = 'lib'
            shlib_ext = 'dylib'
         elif platform == 'cygwin':
            shlib_prefix = 'lib'
            shlib_ext = 'dll'
         else:
            shlib_prefix = 'lib'
            shlib_ext = 'so'

         boost_sub_minor_ver = ver_num % 100
         boost_minor_ver     = ver_num / 100 % 1000
         boost_major_ver     = ver_num / 100000

         version = '-%d_%d' % (boost_major_ver, boost_minor_ver)
         if boost_sub_minor_ver > 0:
            version += '_%d' % boost_sub_minor_ver

         bpl_found = False
         libdirs = ['lib']

         if os.uname()[4] == 'x86_64':
            libdirs.append('lib64')

         full_bpl = 'boost_python%s%s%s%s' % (tool, threading, dbg, version)
         min_bpl  = 'boost_python'
         names = [full_bpl, min_bpl]

         # We would prefer to use the full Boost.Python name, but the search
         # performed below will give us something valid.
         bpl_name = full_bpl

         for l in libdirs:
            for n in names:
               boost_python_lib_name = pj(value, l,
                                          '%s%s.%s' % (shlib_prefix, n,
                                                       shlib_ext))

               print "Checking for '%s'" % boost_python_lib_name
               if os.path.isfile(boost_python_lib_name):
                  print "Using '%s'" % boost_python_lib_name
                  bpl_libdir = l
                  bpl_name   = n
                  bpl_found  = True
                  break

         if not bpl_found:
            print "No Boost.Python library was found in", libdirs
            Exit()
            return False

         if platform == 'irix':
            environ.Append(BoostCPPPATH = [pj(boost_inc_dir),
                                           pj(boost_inc_dir, 'compatibility',
                                              'cpp_c_headers')])
         else:
            environ.Append(BoostCPPPATH = [pj(boost_inc_dir)])

         environ.Append(BoostLIBPATH = [pj(value, bpl_libdir)])
         environ.Append(BoostLIBS = [bpl_name])

   else:
      assert False, "Invalid Boost key"

def ApplyBoostOptions(env):
   global enable_python
   if enable_python:
      env.Append(CPPPATH = env["BoostCPPPATH"])
      env.Append(LIBPATH = env["BoostLIBPATH"])
      env.Append(LIBS = env["BoostLIBS"])

def AddBoostOptions(opts):
   opts.Add('BoostVersion',
            help = 'Boost version number in the form major.minor',
            default = boost_version, validator = ValidateBoostVersion)
   opts.Add('BoostPythonDir',
            help = 'Boost.Python installation directory (boost include dir must exist under this directory)',
            default = '/usr/local', validator = ValidateBoostOption)

def ValidatePythonOption(key, value, environ):
   "Validate the Python option settings"
   global enable_python
   sys.stdout.write("checking for %s [%s]...\n" % (key, value))

   if "EnablePython" == key:
      value = value.lower()
      if value == 'true' or value == '1' or value == 'yes':
         enable_python = True
      else:
         enable_python = False

      if enable_python:
         required_version = 2.2
         python = WhereIs('python')
         if not python:
            enable_python = False
            print 'WARNING: Can\'t find python executable'
            return False

         py_cmd = python + ' -c \'import sys; print sys.prefix; print sys.version[:3]\''
         prefix = ''
         py_ver = ''

         # XXX: Something is broken with trying to use os.popen() on Windows.
         # This is a hack to work around that until something real can be
         # found to fix it.  Basically, this makes the assumption that the
         # Python installation being used to invoke scons is the same as the
         # one against which PyGMTL will be compiled.
         if GetPlatform() == 'win32':
            (prefix, py_ver) = (sys.prefix, sys.version[:3])
         else:
            (prefix, py_ver) = string.split(os.popen(py_cmd).read())

         environ.Append(PythonSHLIBSUFFIX = distutils.sysconfig.get_config_var('SO'))

         # Version must match
         if float(py_ver) < required_version:
            print 'WARNING: Python version ' + py_ver + ' != ' + str(required_version)
            enable_python = False
         else:
            # Build up the env information
            environ.Append(PythonCXXFLAGS = ['-DBOOST_PYTHON_MAX_ARITY=16'])
            if GetPlatform() == 'win32':
               environ.Append(PythonCPPPATH = [pj(prefix, 'include')])
               environ.Append(PythonLIBPATH = [pj(prefix, 'libs')])
            else:
               environ.Append(PythonCPPPATH = [pj(prefix, 'include', 'python'+py_ver)])
               environ.Append(PythonLIBPATH = [])
   else:
      assert False, "Invalid Python key"

   return enable_python

def ApplyPythonOptions(env):
   env.Append(CXXFLAGS = env["PythonCXXFLAGS"])
   env.Append(CPPPATH = env["PythonCPPPATH"])
   env.Append(LIBPATH = env["PythonLIBPATH"])
   env['SHLIBSUFFIX'] = env["PythonSHLIBSUFFIX"]

def AddPythonOptions(opts):
   opts.Add('EnablePython',
            help = 'Enable compilation of PyGMTL; default: EnablePython=False',
            default = False, validator = ValidatePythonOption)

def ValidateCppUnitOption(key, value, environ):
   "Validate the CppUnit option settings"
   global have_cppunit
   sys.stdout.write("checking for %s [%s]...\n" % (key, value))

   if "CppUnitDir" == key:
      cfg = os.path.join(environ['CppUnitDir'], 'bin', 'cppunit-config')
      found = os.path.isfile(cfg)

      if found:
         sys.stdout.write('yes\n')
         have_cppunit = True
      else:
         sys.stdout.write('no\n')
         have_cppunit = False
   else:
      assert False, "Invalid CppUnit key"
      found = 0

   return found

def ApplyCppUnitOptions(env):
   global have_cppunit
   if have_cppunit:
      cfg = pj(env['CppUnitDir'], 'bin', 'cppunit-config')
      env.ParseConfig(cfg + ' --cflags --libs')

def AddCppUnitOptions(opts):
   opts.Add('CppUnitDir',
            help = 'CppUnit installation directory (cppunit dir must exist under this directory": default: CppUnitDir="/usr/local/include"',
            default = '/usr/local/include', validator = ValidateCppUnitOption)

Export('ApplyBoostOptions ApplyPythonOptions ApplyCppUnitOptions')

#------------------------------------------------------------------------------
# Grok the arguments to this build
#------------------------------------------------------------------------------

# Figure out what version of GMTL we're building
GMTL_VERSION = GetGMTLVersion()
print 'Building GMTL Version: %i.%i.%i' % GMTL_VERSION

help_text = "\n---- GMTL Build System ----\n"

# Get command-line arguments
optimize = ARGUMENTS.get('optimize', 'no')
profile = ARGUMENTS.get('profile', 'no')
PREFIX = ARGUMENTS.get('prefix', '/usr/local')
Prefix(PREFIX)
Export('PREFIX')
Export('optimize')
print "Install prefix: ", Prefix()

# Create the extra builders
# Define a builder for the gmtl-config script
builders = {
   'ConfigBuilder'   : Builder(action = CreateConfig)
}
   
baseEnv = BuildPlatformEnv()
baseEnv['enable_python'] = False
Export('baseEnv')

options_cache = 'options.cache.' + distutils.util.get_platform()
opts = Options(options_cache)
AddCppUnitOptions(opts)
AddPythonOptions(opts)
AddBoostOptions(opts)
opts.Add('versioning', 'If no then build headers without versioning', 'yes')

if SCons.Util.WhereIs('distcc'):
   baseEnv.Prepend(CXX = "distcc ", CC = "distcc ")

if not has_help_flag:
   opts.Update(baseEnv);
   opts.Save(options_cache, baseEnv);

help_text += "Platform: " + GetPlatform() + "\n";
help_text += str(baseEnv["TOOLS"]) + "\n";
help_text += opts.GenerateHelpText(baseEnv);
help_text += """\nOther Options:
   optimize=no          Should we build optimize
   profile=no           Should we build profiled code
   prefix=/usr/local    Installation prefix
"""
help_text += """
You can store configuration options in the file: options.custom
This file will be loaded each time.  Note: Options are cached in the file
%s
""" % options_cache

installed_targets = []   # List of targets in the install location
Export('installed_targets')

if not has_help_flag:
   print "Preparing build settings...\n"

   # Create the GMTL package
   pkg = Package('gmtl', '%i.%i.%i' % GMTL_VERSION)
   pkg.addExtraDist(Split("""
      AUTHORS
      ChangeLog
      COPYING
      gmtl-config.in
      gmtl.pc.in
      SConstruct
      docs/Makefile
      docs/docbook.mk
      docs/gmtl.doxy
      docs/gmtl.latex.doxy
      docs/gmtl.man.doxy
      docs/version.mk.doxy
      docs/programmer.guide/Makefile
      docs/programmer.guide/guide.xml
      gmtl/gmtl.doxygen
      python/SConscript
      Test/SConscript
      Test/TestSuite/SConscript
      Test/TestSuite/TestCases/SConscript
      Test/TestSuite/TestCases/InfoTests/SConscript
      tools/build/AutoDist.py
   """))
   Export('pkg')

   # Find gmtl headers, set up install rule and add to package
   gmtl_headers = []
   def get_headers(hdrs, dirname, flist):
      hdrs.extend( [pj(dirname,f) for f in flist if f.endswith('.h')])
   os.path.walk('gmtl',get_headers,gmtl_headers)
   #print "GMTL Headers:\n", gmtl_headers, "\n"

   if baseEnv['versioning'] == 'yes':
      INCLUDE_VERSION= "gmtl-%s.%s.%s" % GetGMTLVersion()
      INCLUDE_DIR = pj('include', INCLUDE_VERSION)
   else:
      INCLUDE_DIR = 'include'
   
   for h in gmtl_headers:
      installed_targets += baseEnv.InstallAs(pj(PREFIX, INCLUDE_DIR, h), h)
      pkg.addExtraDist([File(h)])
   
   # Process subdirectories
   subdirs = []
   if enable_python:
      build_dir = 'build.' + distutils.util.get_platform()
      BuildDir(build_dir, 'python', duplicate = 0)
      subdirs.append(build_dir)
   if have_cppunit:
      subdirs.append('Test')
   SConscript(dirs = subdirs)

   # Setup the builder for gmtl-config
   env = baseEnv.Copy(BUILDERS = builders)
   gmtl_config_submap = {
         '@prefix@'                    : PREFIX,
         '@exec_prefix@'               : '${prefix}',
         '@gmtl_cxxflags@'             : '',
         '@includedir@'                : pj(PREFIX, INCLUDE_DIR),
         '@gmtl_extra_cxxflags@'       : '',
         '@gmtl_extra_include_dirs@'   : '',
         '@VERSION_MAJOR@'             : str(GMTL_VERSION[0]),
         '@VERSION_MINOR@'             : str(GMTL_VERSION[1]),
         '@VERSION_PATCH@'             : str(GMTL_VERSION[2]),
      }
   env.ConfigBuilder('gmtl-config','gmtl-config.in',submap = gmtl_config_submap)
   # The following is commented out because it causes gmtl-config to be
   # regenerated if the value of PREFIX changes between separate build and
   # install invocations. This is a problem when building a GMTL RPM since
   # the RPM is built from a GMTL installation made in a temporary location
   # that is not the same as where it gets installed by an end user.
#   env.Depends('gmtl-config', Value(gmtl_config_submap))
   installed_targets += env.Install(pj(PREFIX, 'bin'), 'gmtl-config')

   # Setup the builder for gmtl-config
   env = baseEnv.Copy(BUILDERS = builders)
   gmtl_pc_submap = {
         '@prefix@'                    : PREFIX,
         '@exec_prefix@'               : '${prefix}',
         '@gmtl_cxxflags@'             : '',
         '@includedir@'                : pj(PREFIX, INCLUDE_DIR),
         '@gmtl_extra_cxxflags@'       : '',
         '@gmtl_extra_include_dirs@'   : '',
         '@version_major@'             : str(GMTL_VERSION[0]),
         '@version_minor@'             : str(GMTL_VERSION[1]),
         '@version_patch@'             : str(GMTL_VERSION[2]),
      }
   env.ConfigBuilder('gmtl.pc','gmtl.pc.in',submap = gmtl_pc_submap)
   installed_targets += env.Install(pj(PREFIX, 'share', 'pkgconfig'), 'gmtl.pc')

   pkg.build()
   installed_targets += pkg.getInstalledTargets()
   MakeSourceDist(pkg, env)

# Build everything by default
Default('.')
Alias('install',installed_targets)
Help(help_text)
